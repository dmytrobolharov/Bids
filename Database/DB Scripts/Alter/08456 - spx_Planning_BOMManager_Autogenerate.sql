IF  EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[spx_Planning_BOMManager_Autogenerate]') AND type in (N'P', N'PC'))
DROP PROCEDURE [dbo].[spx_Planning_BOMManager_Autogenerate]
GO

CREATE PROCEDURE [dbo].[spx_Planning_BOMManager_Autogenerate]
(
	  @PlanningID UNIQUEIDENTIFIER
	, @SeasonYearID UNIQUEIDENTIFIER
	, @TeamID UNIQUEIDENTIFIER
)
AS
BEGIN
	DECLARE @WorkflowID UNIQUEIDENTIFIER = '40000000-0000-0000-0000-000000000080'
	DECLARE @CreatedBy NVARCHAR(100) = (SELECT ISNULL(FirstName,'') + ' ' + ISNULL(LastName, '') FROM Users WHERE TeamID = @TeamID)
	DECLARE @CreatedDate DATETIME = GETDATE()
	DECLARE @WorkflowItemTypeID UNIQUEIDENTIFIER = (SELECT WorkflowItemTypeId FROM pWorkflowItemType WHERE MapId = @WorkflowID)

	-- Change Log
	DECLARE @ChangeManagement nVARCHAR(5) = (SELECT UPPER(RTRIM(LTRIM(AppSettingValue))) FROM sAppSetting WHERE AppSettingKey = 'ChangeManagementEnabled')	
	DECLARE @ChangeLog TABLE(
		  ChangeTableID UNIQUEIDENTIFIER
		, ChangeTablePKID UNIQUEIDENTIFIER
		, ChangeFieldName NVARCHAR(200)
		, ChangeFieldAlias NVARCHAR(200)
		, ChangeBeforeValue NVARCHAR(200)
		, ChangeAfterValue NVARCHAR(200)
		, ChangeBeforeText NVARCHAR(4000)
		, ChangeAfterText NVARCHAR(4000)
		, ChangeSubject NVARCHAR(400)
		, ChangeType INT)	
	
	DECLARE CUR CURSOR FOR
	SELECT DISTINCT
		pli.StyleID
		, sh.StyleSet
		, NEWID() AS WorkflowItemID
		, pli.PlanningItemID
	FROM vwx_PlanningItem_SELECT pli
	INNER JOIN pStyleHeader sh ON pli.StyleID = sh.StyleID
	INNER JOIN pStyleSeasonYear ssy ON pli.StyleSeasonYearID = ssy.StyleSeasonYearID
	INNER JOIN pStyleWorkflow sw ON pli.StyleSeasonYearID = sw.StyleSeasonYearID AND sw.WorkflowID = @WorkflowID
	LEFT JOIN pStyleBOMDimension sbd
		INNER JOIN pWorkflowItem wfi ON sbd.WorkflowItemID = wfi.WorkflowItemID
	ON pli.StyleBOMDimensionID = sbd.StyleBOMDimensionID 
		AND pli.StyleSeasonYearID = wfi.StyleSeasonYearID
	WHERE
		pli.TeamId = @TeamID
		AND pli.PlanningID = @PlanningID
		AND ssy.SeasonYearID = @SeasonYearID
		AND pli.PlanningItemDrop = 'No'
		AND (sbd.StyleBOMDimensionID IS NULL OR sbd.StyleBOMDimensionID = '00000000-0000-0000-0000-000000000000')
	
	OPEN CUR
	DECLARE @StyleID UNIQUEIDENTIFIER, @StyleSet INT, @WorkflowItemID UNIQUEIDENTIFIER, @PlanningItemID UNIQUEIDENTIFIER
	FETCH NEXT FROM CUR INTO  @StyleID, @StyleSet, @WorkflowItemID, @PlanningItemID
	WHILE @@FETCH_STATUS = 0
	BEGIN
		DECLARE @WorkflowItemName NVARCHAR(200) = N'BOM ' + CONVERT(varchar, (SELECT count(DISTINCT WorkFlowItemName) FROM pWorkFlowItem WHERE WorkFlowItemName LIKE 'BOM%' AND StyleID = @StyleID) +1)
	
		EXEC spx_WorkFlowItem_Seasonal_INSERT @WorkflowItemID, @WorkflowID, @WorkflowItemTypeID, @StyleID, @StyleSet, @WorkflowItemName, @CreatedBy, @CreatedDate, @SeasonYearID
		EXEC spx_PlanningItem_BOM_LINK @PlanningItemID, @WorkflowItemID
		
		IF @ChangeManagement = 'TRUE'
		BEGIN
			INSERT INTO @ChangeLog(
				ChangeTableID, ChangeTablePKID, ChangeFieldName, ChangeFieldAlias, 
				ChangeBeforeValue, ChangeAfterValue, ChangeBeforeText, ChangeAfterText, 
				ChangeSubject, ChangeType)
			SELECT 'CCC0490A-44C8-47D3-AD63-3021DAA13142', @PlanningID, 'PlanningID', sh.StyleNo + ' - Bill Of Material (Autogenerated)',
				'', sbd.StyleBOMDimensionID, '', @WorkflowItemName, '', 1
			FROM pStyleHeader sh 
			INNER JOIN pStyleBOMDimension sbd ON sbd.WorkFlowItemID = @WorkflowItemID
			WHERE sh.StyleID = @StyleID
		END
		
		FETCH NEXT FROM CUR INTO  @StyleID, @StyleSet, @WorkflowItemID, @PlanningItemID
	END
	CLOSE CUR
	DEALLOCATE CUR
	
	SELECT * FROM @ChangeLog

END



GO


INSERT INTO sVersion(AppName, Version, LastScriptRun, TimeStamp)
VALUES ('DB_Version', '0.5.0.0000', '08456', GetDate())
GO
